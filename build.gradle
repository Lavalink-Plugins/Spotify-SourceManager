allprojects {
    group = "com.github.TopiSenpai.LavaSrc"

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven { url "https://m2.dv8tion.net/releases" }
        jcenter()
    }
}

def getGitVersion() {
    def versionStr = new ByteArrayOutputStream()
    def result = exec {
        standardOutput versionStr
        errorOutput versionStr
        ignoreExitValue true
        commandLine "git", "describe", "--exact-match", "--tags"
    }
    if (result.exitValue == 0) {
        return versionStr.toString().trim()
    }


    versionStr = new ByteArrayOutputStream()
    exec {
        standardOutput versionStr
        errorOutput versionStr
        commandLine "git", "rev-parse", "--short", "HEAD"
    }

    return versionStr.toString().trim()
}

var isMavenDefined = findProperty("MAVEN_USERNAME") != null && findProperty("MAVEN_PASSWORD") != null

subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    version getGitVersion()

    println "Version: " + version

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    compileJava.options.encoding = "UTF-8"

    publishing {
        publications {
            maven(MavenPublication) {
                groupId = group
                artifactId = project.archivesBaseName
                version = project.version

                from components.java
            }
        }
        if (isMavenDefined) {
            System.out.println("Publishing to Maven Repo")
            repositories {
                maven {
                    name = "Reposilite"
                    url = "https://maven.topi.wtf/releases"
                    credentials {
                        username = findProperty("MAVEN_USERNAME")
                        password = findProperty("MAVEN_PASSWORD")
                    }
                }
            }
        }
    }
}
